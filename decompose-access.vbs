'
' Usage: decompose-access.vbs DATABASE [OUTPUT FOLDER]
'
' Generates a set of source files and a mainline build script compose-XXX.vbs
' which can be used to rebuild the specified database.
'
' Inspired by Oliver:
' http://stackoverflow.com/questions/187506/how-do-you-use-version-control-with-access-development


Option Explicit

Const acCmdAppMaximize = 10
Const acForm = 2 
Const acModule = 5 
Const acMacro = 4 
Const acReport = 3 


Dim fso, sInputFile, sOutputFolder, oAccess

If (WScript.Arguments.Count <> 1 And WScript.Arguments.Count <> 2) Then 
	Usage()
	WScript.Quit() 
End If

Set fso = CreateObject("Scripting.FileSystemObject")
sInputFile = WScript.Arguments(0)

If WScript.Arguments.Count = 2 Then
	sOutputFolder = WScript.Arguments(1)
Else
	sOutputFolder = fso.GetParentFolderName(sInputFile)
End If

Set oAccess = CreateObject("Access.Application")

' If database is unsigned and needs to show the "Security Warning" dialog,
' then the following commands are needed to prevent the annoying
' "half screen" behaviour.
oAccess.Visible = True
oAccess.RunCommand acCmdAppMaximize

oAccess.OpenCurrentDatabase sInputFile
DecomposeDatabase oAccess, sOutputFolder
oAccess.CloseCurrentDatabase
oAccess.Quit 


Function Usage()
	MsgBox "Usage: decompose-access.vbs DATABASE [OUTPUT FOLDER]", vbInformation, "Decompose Access"
End Function


Function DecomposeDatabase(oAccess, sOutputFolder)
	Dim sScriptFile, oScriptStream

	sScriptFile = fso.BuildPath(sOutputFolder, "compose-" & fso.GetBaseName(oAccess.CurrentProject.Name) & ".vbs")
	Set oScriptStream = fso.CreateTextFile(sScriptFile, True)
	oScriptStream.WriteLine "'"
	oScriptStream.WriteLine "' DO NOT EDIT THIS FILE."
	oScriptStream.WriteLine "' Automatically generated by decompose-access.vbs"
	oScriptStream.WriteLine "'"
	oScriptStream.WriteLine

	ScriptCreate oAccess, oScriptStream
	DecomposeObjects oAccess, oScriptStream, sOutputFolder
	ScriptClose oAccess, oScriptStream

	oScriptStream.Close
End Function


Function DecomposeObjects(oAccess, oScriptStream, sOutputFolder)
	Dim obj, sFolderName, sFolderPath, sFileName

	sFolderName = fso.GetBaseName(oAccess.CurrentProject.Name) & "-source"
	sFolderPath = fso.BuildPath(sOutputFolder, sFolderName)
	If Not fso.FolderExists(sFolderPath) Then
		fso.CreateFolder(sFolderPath)
	End If

	For Each obj In oAccess.CurrentProject.AllForms
		sFileName = obj.FullName & ".ADF"
		oAccess.SaveAsText acForm, obj.FullName, fso.BuildPath(sFolderPath, sFileName)
		oScriptStream.WriteLine "oAccess.LoadFromText " & CStr(acForm) & ", """ & obj.FullName & """, oAccess.CurrentProject.Path & ""\" & fso.BuildPath(sFolderName, sFileName) & """"
	Next
	oScriptStream.WriteLine
End Function


Function ScriptCreate(oAccess, oScriptStream)
	Dim sConnectionString

	sConnectionString = "Provider=" & oAccess.CurrentProject.Connection.Provider _
		& ";Data Source=" & oAccess.CurrentProject.Name _
		& ";Jet OLEDB:Engine Type=" & oAccess.CurrentProject.Connection.Properties("Jet OLEDB:Engine Type")

	oScriptStream.WriteLine "Set oCatalog = CreateObject(""ADOX.Catalog"")"
	oScriptStream.WriteLine "oCatalog.Create """ & sConnectionString & """"
	oScriptStream.WriteLine "oCatalog.ActiveConnection.Close"
	oScriptStream.WriteLine
	oScriptStream.WriteLine "Set oAccess = CreateObject(""Access.Application"")"
	oScriptStream.WriteLine "oAccess.Visible = True"
	oScriptStream.WriteLine "oAccess.RunCommand " & CStr(acCmdAppMaximize)
	oScriptStream.WriteLine "oAccess.OpenCurrentDatabase CreateObject(""WScript.Shell"").CurrentDirectory & ""\" & oAccess.CurrentProject.Name & """"
	oScriptStream.WriteLine
End Function


Function ScriptClose(oAccess, oScriptStream)
	oScriptStream.WriteLine "oAccess.CloseCurrentDatabase"
	oScriptStream.WriteLine "oAccess.Quit"
	oScriptStream.WriteLine
End Function
