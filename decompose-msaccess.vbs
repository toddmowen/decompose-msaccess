'
' Usage: decompose-msaccess.vbs DATABASE [OUTPUT FOLDER]
'
' Generates a set of source files and a mainline build script compose-XXX.vbs
' which can be used to rebuild the specified database.
'
' Based on code by Oliver:
' http://stackoverflow.com/questions/187506/how-do-you-use-version-control-with-access-development


Option Explicit

Const acCmdAppMaximize = 10
Const acForm = 2 
Const acModule = 5 
Const acMacro = 4 
Const acReport = 3 


Dim fso, sInputFile, sOutputFolder, oAccess

If (WScript.Arguments.Count <> 1 And WScript.Arguments.Count <> 2) Then 
	Usage()
	WScript.Quit() 
End If

Set fso = CreateObject("Scripting.FileSystemObject")
sInputFile = fso.GetAbsolutePathName(WScript.Arguments(0))

If WScript.Arguments.Count = 2 Then
	sOutputFolder = WScript.Arguments(1)
Else
	sOutputFolder = fso.GetParentFolderName(sInputFile)
End If

Set oAccess = CreateObject("Access.Application")

' If database is unsigned and needs to show the "Security Warning" dialog,
' then it appears in a window half the screen width, and unless we maximize
' now then this annoying window size will be remembered next time Access
' is run.
oAccess.Visible = True
oAccess.RunCommand acCmdAppMaximize
oAccess.Visible = False

oAccess.OpenCurrentDatabase sInputFile
DecomposeDatabase oAccess, sOutputFolder
oAccess.CloseCurrentDatabase
oAccess.Quit 

MsgBox sInputFile + " decomposed to folder " + sOutputFolder + ".", vbInformation, "Decompose MS-Access"
WScript.Quit()


Function Usage()
	MsgBox "Usage: decompose-msaccess.vbs DATABASE [OUTPUT FOLDER]", vbInformation, "Decompose MS-Access"
End Function


Function DecomposeDatabase(oAccess, sOutputFolder)
	Dim sScriptFile, oScriptStream

	sScriptFile = fso.BuildPath(sOutputFolder, "compose-" & fso.GetBaseName(oAccess.CurrentProject.Name) & ".vbs")
	Set oScriptStream = fso.CreateTextFile(sScriptFile, True)
	oScriptStream.WriteLine "'"
	oScriptStream.WriteLine "' DO NOT EDIT THIS FILE."
	oScriptStream.WriteLine "' Automatically generated by decompose-msaccess.vbs"
	oScriptStream.WriteLine "'"
	oScriptStream.WriteLine

	ScriptCreate oAccess, oScriptStream
	DecomposeObjects oAccess, oScriptStream, sOutputFolder
	ScriptClose oAccess, oScriptStream

	oScriptStream.Close
End Function


Function DecomposeObjects(oAccess, oScriptStream, sOutputFolder)
	Dim obj, sFolderName, sFolderPath, sFileName

	sFolderName = fso.GetBaseName(oAccess.CurrentProject.Name) & "-source"
	sFolderPath = fso.BuildPath(sOutputFolder, sFolderName)
	If Not fso.FolderExists(sFolderPath) Then
		fso.CreateFolder(sFolderPath)
	End If

	For Each obj In oAccess.CurrentProject.AllForms
		sFileName = obj.FullName & ".ADF"
		oAccess.SaveAsText acForm, obj.FullName, fso.BuildPath(sFolderPath, sFileName)
		oScriptStream.WriteLine "oAccess.LoadFromText " & CStr(acForm) & ", """ & obj.FullName & """, fso.BuildPath(sInputFolder, """ & sFolderName & "\" & obj.FullName & ".ADF"")"
	Next
	oScriptStream.WriteLine
End Function


Function ScriptCreate(oAccess, oScriptStream)
	Dim sConnectionString

	sConnectionString = "Provider=" & oAccess.CurrentProject.Connection.Provider _
		& ";Data Source="" & sOutputFile & """ _
		& ";Jet OLEDB:Engine Type=" & oAccess.CurrentProject.Connection.Properties("Jet OLEDB:Engine Type")

	oScriptStream.WriteLine "Set fso = CreateObject(""Scripting.FileSystemObject"")"
	oScriptStream.WriteLine "sInputFolder = fso.GetParentFolderName(WScript.ScriptFullName)"
	oScriptStream.WriteLine "If WScript.Arguments.Count = 1 Then"
	oScriptStream.WriteLine "    sOutputFile = fso.GetAbsolutePathName(WScript.Arguments(0))"
	oScriptStream.WriteLine "Else"
	oScriptStream.WriteLine "    sOutputFile = fso.BuildPath(sInputFolder, """ & oAccess.CurrentProject.Name & """)"
	oScriptStream.WriteLine "End If"
	oScriptStream.WriteLine
	oScriptStream.WriteLine "Set oCatalog = CreateObject(""ADOX.Catalog"")"
	oScriptStream.WriteLine "oCatalog.Create """ & sConnectionString & """"
	oScriptStream.WriteLine "oCatalog.ActiveConnection.Close"
	oScriptStream.WriteLine
	oScriptStream.WriteLine "Set oAccess = CreateObject(""Access.Application"")"
	oScriptStream.WriteLine "oAccess.Visible = True"
	oScriptStream.WriteLine "oAccess.RunCommand " & CStr(acCmdAppMaximize)
	oScriptStream.WriteLine "oAccess.Visible = False"
	oScriptStream.WriteLine "oAccess.OpenCurrentDatabase sOutputFile"
	oScriptStream.WriteLine
End Function


Function ScriptClose(oAccess, oScriptStream)
	oScriptStream.WriteLine "MsgBox ""Re-composed "" + oAccess.CurrentProject.FullName + ""."", vbInformation, ""Decompose MS-Access"""
	oScriptStream.WriteLine "oAccess.CloseCurrentDatabase"
	oScriptStream.WriteLine "oAccess.Quit"
	oScriptStream.WriteLine
End Function
